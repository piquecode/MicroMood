plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
}

android {
    namespace 'com.piquecode.micromood'
    compileSdk 34
    
    dependenciesInfo {
        // Disables dependency metadata when building APKs (for IzzyOnDroid/F-Droid)
        includeInApk = false
        // Disables dependency metadata when building Android App Bundles (for Google Play)
        includeInBundle = false
    }
    
    def apiKeys = loadApiKeys()
    
    // Load keystore properties
    def keystorePropertiesFile = rootProject.file("keystore.properties")
    def keystoreProperties = new Properties()
    def hasKeystore = false
    
    if (keystorePropertiesFile.exists()) {
        keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
        hasKeystore = keystoreProperties['storeFile'] != null && 
                      new File(keystoreProperties['storeFile']).exists()
    }

    defaultConfig {
        applicationId "com.piquecode.micromood"
        minSdkVersion 21
        targetSdk 34
        versionCode 3
        versionName "1.2.0"

        vectorDrawables.useSupportLibrary true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField "String", "SENTRY_DSN", apiKeys['SENTRY_DSN']
    }

    signingConfigs {
        release {
            if (hasKeystore) {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (hasKeystore) {
                signingConfig signingConfigs.release
            }
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
    }

    testOptions {
        unitTests {
            includeAndroidResources true
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    buildFeatures {
        buildConfig = true
        dataBinding true
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion '1.5.10'
    }
}

dependencies {
    def lifecycle_version = "2.7.0"
    def arch_version = "2.2.0"
    def room_version = "2.6.1"
    def espresso_version = "3.5.1"
    def junit_version = "1.1.5"

    implementation fileTree(dir: "libs", include: ["*.jar"])

    // Core
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.9.22"
    implementation 'androidx.core:core-ktx:1.13.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3'

    // Compose
    def composeBom = platform('androidx.compose:compose-bom:2024.01.00')
    implementation composeBom
    androidTestImplementation composeBom

    implementation 'androidx.compose.material3:material3'

    // Compose - Android Studio Preview support
    implementation 'androidx.compose.ui:ui-tooling-preview'
    debugImplementation 'androidx.compose.ui:ui-tooling'

    // Hilt
    implementation "com.google.dagger:hilt-android:2.48"
    kapt "com.google.dagger:hilt-compiler:2.48"

    // Jetpack
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.preference:preference-ktx:1.2.1"

    // UI
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.applandeo:material-calendar-view:1.9.0-rc03'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'nl.dionsegijn:konfetti:1.2.1'

    // Support
    implementation 'io.sentry:sentry-android:7.11.0'

    // Testing
    testImplementation "androidx.test.ext:junit:$junit_version"
    testImplementation "androidx.arch.core:core-testing:$arch_version"
    testImplementation "androidx.room:room-testing:$room_version"
    testImplementation "androidx.test.espresso:espresso-core:$espresso_version"
    testImplementation "androidx.test.espresso:espresso-intents:$espresso_version"
    testImplementation 'junit:junit:4.13.2'
    testImplementation "com.google.truth:truth:1.1"
    testImplementation "io.mockk:mockk:1.13.3"
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.7.3'
    testImplementation 'org.robolectric:robolectric:4.10'
    testImplementation 'com.google.dagger:hilt-android-testing:2.48'
    kaptTest 'com.google.dagger:hilt-android-compiler:2.48'

    androidTestImplementation "androidx.test.ext:junit:$junit_version"
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_version"

    // Compose - UI Tests
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'
}

kapt {
    correctErrorTypes true
    javacOptions {
        option("-source", "17")
        option("-target", "17")
    }
}

static def loadApiKeys() {
    Properties properties = new Properties()
    File secretsFile = new File("secrets.properties")
    if (secretsFile.exists()) {
        properties.load(new FileInputStream(secretsFile))
    } else {
        properties.setProperty('SENTRY_DSN', '""')
    }
    return properties
}